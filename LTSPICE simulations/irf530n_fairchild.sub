
.SUBCKT IRF530N_fairchild 2 1 3 ; rev 15 July 2001
CA 12 8 1.27e-9
CB 15 14 1.27e-9
CIN 6 8 7.20e-10
DBODY 7 5 DBODYMOD
DBREAK 5 11 DBREAKMOD
DPLCAP 10 5 DPLCAPMOD
EBREAK 11 7 17 18 117.8
EDS 14 8 5 8 1
EGS 13 8 6 8 1
ESG 6 10 6 8 1
EVTHRES 6 21 19 8 1
EVTEMP 20 6 18 22 1
IT 8 17 1
LDRAIN 2 5 1.0e-9
LGATE 1 9 5.53e-9
LSOURCE 3 7 4.35e-9
MMED 16 6 8 8 MMEDMOD
MSTRO 16 6 8 8 MSTROMOD
MWEAK 16 21 8 8 MWEAKMOD
RBREAK 17 18 RBREAKMOD 1
RDRAIN 50 16 RDRAINMOD 2.70e-2
RGATE 9 20 2.50
RLDRAIN 2 5 10
RLGATE 1 9 55.3
RLSOURCE 3 7 43.5
RSLC1 5 51 RSLCMOD 1e-6
RSLC2 5 50 1e3
RSOURCE 8 7 RSOURCEMOD 1.77e-2
RVTHRES 22 8 RVTHRESMOD 1
RVTEMP 18 19 RVTEMPMOD 1
S1A 6 12 13 8 S1AMOD
S1B 13 12 13 8 S1BMOD
S2A 6 15 14 13 S2AMOD
S2B 13 15 14 13 S2BMOD
VBAT 22 19 DC 1
ESLC 51 50 VALUE={(V(5,51)/ABS(V(5,51)))*(PWR(V(5,51)/(1e-6*43.5),3.5))}
.MODEL DBODYMOD D (IS = 6.0e-13 RS = 6.2e-3 XTI = 5.5 TRS1 = 2.1e-3 TRS2 = 2.0e-6 CJO = 8.50e-10 TT = 6.30e-8 M = 0.54)
.MODEL DBREAKMOD D (RS = 5.6e-1 TRS1 = 8e-4 TRS2 = 3e-6)
.MODEL DPLCAPMOD D (CJO = 9.29e-10 IS = 1e-30 M = 0.79)
.MODEL MMEDMOD NMOS (VTO = 3.21 KP = 5 IS = 1e-30 N = 10 TOX = 1 L = 1u W = 1u RG = 2.50)
.MODEL MSTROMOD NMOS (VTO = 3.60 KP = 37 IS = 1e-30 N = 10 TOX = 1 L = 1u W = 1u)
.MODEL MWEAKMOD NMOS (VTO = 2.77 KP = 0.09 IS = 1e-30 N = 10 TOX = 1 L = 1u W = 1u RG = 25.0 )
.MODEL RBREAKMOD RES (TC1 =1.05e-3 TC2 = -5e-7)
.MODEL RDRAINMOD RES (TC1 = 1.20e-2 TC2 = 3.00e-5)
.MODEL RSLCMOD RES (TC1 = 3.20e-3 TC2 = 3.00e-6)
.MODEL RSOURCEMOD RES (TC1 = 1e-3 TC2 = 1e-6)
.MODEL RVTHRESMOD RES (TC1 = -2.20e-3 TC2 = -9.00e-6)
.MODEL RVTEMPMOD RES (TC1 = -2.40e-3 TC2 =1.80e-6)
.MODEL S1AMOD VSWITCH (RON = 1e-5 ROFF = 0.1 VON = -6.2 VOFF= -3.1)
.MODEL S1BMOD VSWITCH (RON = 1e-5 ROFF = 0.1 VON = -3.1 VOFF= -6.2)
.MODEL S2AMOD VSWITCH (RON = 1e-5 ROFF = 0.1 VON = -1.0 VOFF= 0.5)
.MODEL S2BMOD VSWITCH (RON = 1e-5 ROFF = 0.1 VON = 0.5 VOFF= -1.0)
.ENDS